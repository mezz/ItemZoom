buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'

version = version_major + '.' + version_minor
group = "mezz.itemzoom" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "itemzoom" + mcversion

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_6 // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_6
}

minecraft {
    version = mcversion + "-" + forgeversion
    runDir = "run"
    replace '@VERSION@', project.version
    mappings = mcp_mappings
}

repositories {
    maven {
        name = "jei"
        url "http://dvs1.progwml6.com/files/maven"
    }
}

dependencies {
    deobfCompile "mezz.jei:jei_${mcversion}:${jei_version}:api"
    runtime "mezz.jei:jei_${mcversion}:${jei_version}"
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }

    // Move access transformers to META-INF
    rename '(.+_at.cfg)', 'META-INF/$1'
}

jar {
    manifest {
        attributes 'FMLAT': 'itemzoom_at.cfg'
    }
}
